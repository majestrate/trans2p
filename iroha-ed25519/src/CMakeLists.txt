set(LIBED25519_VERSION "${SOVERSION}-${EDIMPL}-${HASH}-${RANDOM}")
foreach(i EDIMPL HASH RANDOM)
  foreach(j SOURCES INCLUDES LINK_LIBRARIES COMPILE_DEFINITIONS)
    get_property (LIB_${${i}}_${j} GLOBAL PROPERTY LIB_${${i}}_${j})
  endforeach()
endforeach()

add_library(ed25519 ${BUILD}
  ed25519.c
  ${LIB_${EDIMPL}_SOURCES}
  ${LIB_${HASH}_SOURCES}
  ${LIB_${RANDOM}_SOURCES}
  )
set_target_properties(ed25519 PROPERTIES
  LINK_FLAGS -Wl,--version-script=${CMAKE_SOURCE_DIR}/exportmap
  )
target_include_directories(ed25519 PUBLIC
  $<BUILD_INTERFACE:${CMAKE_SOURCE_DIR}/include>
  $<INSTALL_INTERFACE:include>
  ${LIB_${EDIMPL}_INCLUDES}
  ${LIB_${HASH}_INCLUDES}
  ${LIB_${RANDOM}_INCLUDES}
  )
target_link_libraries(ed25519 PUBLIC
  ${LIB_${EDIMPL}_LINK_LIBRARIES}
  ${LIB_${HASH}_LINK_LIBRARIES}
  ${LIB_${RANDOM}_LINK_LIBRARIES}
  )
target_compile_definitions(ed25519 PUBLIC
  ${LIB_${EDIMPL}_COMPILE_DEFINITIONS}
  ${LIB_${HASH}_COMPILE_DEFINITIONS}
  ${LIB_${RANDOM}_COMPILE_DEFINITIONS}
  )
set_target_properties(ed25519 PROPERTIES
  VERSION   ${SOVERSION}
  SOVERSION ${SOVERSION}
  INTERFACE_ed25519_MAJOR_VERSION ${ED25519_MAJOR_VERSION}
  PUBLIC_HEADER ${CMAKE_SOURCE_DIR}/include/ed25519/ed25519.h
  )
set_property(
  TARGET ed25519
  APPEND PROPERTY
  COMPATIBLE_INTERFACE_STRING ed25519_MAJOR_VERSION
)
target_compile_definitions(ed25519 PUBLIC
  -DLIBED25519_VERSION=${LIBED25519_VERSION}
  )

install(TARGETS ed25519 EXPORT ed25519Config
  LIBRARY DESTINATION       ${CMAKE_INSTALL_LIBDIR}
  ARCHIVE DESTINATION       ${CMAKE_INSTALL_LIBDIR}/static
  RUNTIME DESTINATION       ${CMAKE_INSTALL_BINDIR}
  INCLUDES DESTINATION      ${CMAKE_INSTALL_INCLUDEDIR}
  PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
  FRAMEWORK DESTINATION     ${CMAKE_INSTALL_PREFIX}
  )
install(
  DIRECTORY ${CMAKE_SOURCE_DIR}/include/
  DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
)
install(EXPORT ed25519Config DESTINATION share/ed25519/cmake)
export(TARGETS ed25519 FILE ed25519Config.cmake)
